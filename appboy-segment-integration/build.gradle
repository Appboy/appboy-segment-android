apply plugin: 'com.android.library'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

// Grabs the first line of the changelog to get the current version.
// Expects the line to start with Markdown syntax
// (ex: ## major.minor.build)
new File("../public/CHANGELOG.md").withReader { version = it.readLine().substring(3) }
logger.lifecycle("Setting version to last recorded version in CHANGELOG.md: " + version)

allprojects {
  version = rootProject.version
}

apply from: '../support/extra-gradle/appboy-release.gradle'
apply from: '../support/extra-gradle/artifactory-release.gradle'
apply from: '../appboy-segment-integration/gradle/break-compile-on-deprecations.gradle'

buildscript {
  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:${ANDROID_BUILD_TOOLS_GRADLE_VERSION}"
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:${JFROG_BUILD_INFO_EXTRACTOR_VERSION}"
  }
}

android {
  compileSdkVersion 31
  buildToolsVersion '30.0.2'

  defaultConfig {
    // Segment.io supports down to api 14:  https://segment.com/docs/libraries/android/
    // The Braze SDK supports down to api 16
    minSdkVersion 16
    targetSdkVersion 31

    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  testOptions {
    unitTests {
      returnDefaultValues = true
      includeAndroidResources = true
    }
  }
}

checkstyle {
  configFile rootProject.file('gradle/checkstyle.xml')
}

dependencies {
  repositories {
    mavenCentral()
    maven { url "http://appboy.github.io/appboy-android-sdk/sdk" }
    google()
  }

  compileOnly 'com.segment.analytics.android:analytics:4.10.4'

  api 'com.braze:android-sdk-ui:26.3.0'

  testImplementation 'junit:junit:4.12'

  testImplementation 'com.segment.analytics.android:analytics-tests:4.3.1'
  androidTestImplementation 'com.segment.analytics.android:analytics-tests:4.3.1'

  testImplementation "androidx.test:core:1.3.0"
  testImplementation "junit:junit:4.12"
  testImplementation "org.robolectric:robolectric:4.4"
  testImplementation "org.mockito:mockito-inline:3.12.4"

  androidTestImplementation 'androidx.test.ext:junit:1.1.2'
  androidTestImplementation 'androidx.test:rules:1.3.0'
  androidTestImplementation 'junit:junit:4.12'
}

task javadocRelease(type: Javadoc) {
  source(android.sourceSets.main.java.srcDirs)
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  failOnError false
  destinationDir = file("build/outputs/javadoc/")
}
